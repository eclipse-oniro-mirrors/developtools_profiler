# Copyright (c) Huawei Technologies Co., Ltd. 2021. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../base/config.gni")

ohos_source_set("memdataplugin_source") {
  part_name = "${OHOS_PROFILER_PART_NAME}"
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  sources = [
    "src/buffer_splitter.cpp",
    "src/memory_data_plugin.cpp",
    "src/memory_module.cpp",
    "src/smaps_stats.cpp",
  ]
  include_dirs = [
    "include",
    "../api/include",
    "${OHOS_PROFILER_DIR}/interfaces/kits",
    "${OHOS_PROFILER_DIR}/device/base/include",
  ]
  deps = [
    "${OHOS_PROFILER_DIR}/proto_encoder:proto_encoder_source",
    "${OHOS_PROFILER_DIR}/protos/types/plugins/memory_data:memory_data_cpp",
    "${OHOS_PROFILER_DIR}/protos/types/plugins/memory_data:memory_data_encoder",
  ]
  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "c_utils:utils",
      "drivers_interface_memorytracker:libmemorytracker_proxy_1.0",
      "hidumper:lib_dump_usage",
      "hilog:libhilog",
      "hiview:libucollection_graphic",
      "protobuf:protobuf_lite",
    ]
  }
  public_configs = [ "${OHOS_PROFILER_DIR}/device/base:hiprofiler_test_config" ]
}

ohos_shared_library("memdataplugin") {
  output_name = "memdataplugin"
  version_script = "libmemory_plugin.map"
  deps = [ ":memdataplugin_source" ]
  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
    external_deps = [
      "c_utils:utils",
      "drivers_interface_memorytracker:libmemorytracker_proxy_1.0",
      "hilog:libhilog",
    ]
  }
  install_enable = true
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}

ohos_executable("memorytest") {
  output_name = "memorytest"
  sources = [ "src/test_main.cpp" ]

  include_dirs = [ "${OHOS_PROFILER_DIR}/device/base/include" ]

  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "hilog:libhilog",
    ]
  }
  install_enable = false
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}

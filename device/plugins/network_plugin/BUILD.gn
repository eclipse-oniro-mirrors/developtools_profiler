# Copyright (c) Huawei Technologies Co., Ltd. 2021. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../base/config.gni")

ohos_source_set("networkplugin_source") {
  part_name = "${OHOS_PROFILER_PART_NAME}"
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  sources = [
    "../memory_plugin/src/buffer_splitter.cpp",
    "src/network_module.cpp",
    "src/network_plugin.cpp",
  ]
  include_dirs = [
    "include",
    "../api/include",
    "../memory_plugin/include",
    "${OHOS_PROFILER_DIR}/interfaces/kits",
    "${OHOS_PROFILER_DIR}/device/base/include",
  ]
  deps = [
    "${OHOS_PROFILER_DIR}/proto_encoder:proto_encoder_source",
    "${OHOS_PROFILER_DIR}/protos/types/plugins/network_data:network_data_cpp",
    "${OHOS_PROFILER_DIR}/protos/types/plugins/network_data:network_data_encoder",
  ]
  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "bundle_framework:appexecfwk_base",
      "bundle_framework:appexecfwk_core",
      "c_utils:utils",
      "hisysevent:libhisysevent",
      "init:libbegetutil",
      "ipc:ipc_core",
      "netmanager_base:net_stats_manager_if",
      "os_account:os_account_innerkits",
      "protobuf:protobuf_lite",
      "samgr:samgr_proxy",
    ]
  }
  public_configs = [ "${OHOS_PROFILER_DIR}/device/base:hiprofiler_test_config" ]
}
ohos_shared_library("networkplugin") {
  output_name = "networkplugin"
  version_script = "libnetwork_plugin.map"
  deps = [
    ":networkplugin_source",
    "${OHOS_PROFILER_DIR}/device/base:hiprofiler_base",
  ]
  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
  }
  install_enable = true
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}
